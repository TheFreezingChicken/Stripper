plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}


ext {
    ARTIFACT_ID_SUFFIX = 'compose'
    VERSION = '0.2.0'
    // Swap comment state of these two lines for releases.
    //PRE_RELEASE_VERSION_SUFFIX = ""
    PRE_RELEASE_VERSION_SUFFIX = buildSnapshotSuffix()

    // Bump the minor version after a new release branch. Bump major version when starting to work on major changes.
    // In version codes, the two Tag (T) digits are used for special use cases (e.g. releasing a fix to a specific user,
    // creating a new release variant, etc...), so for now they will stay to 00.
    //
    // Version codes: T = Tag, p = Patch, m = minor, M = Major
    //                         M m p T
    //                Max = 2100000000
    ANDROID_VERSION_CODE =       20000
}


android {
    namespace 'me.thefreezingchicken.stripper.compose'
    compileSdk 33

    defaultConfig {
        minSdk 23
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion "$COMPOSE_COMPILER_VERSION"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    def composeBom = platform('androidx.compose:compose-bom:2022.10.00')

    implementation 'androidx.core:core-ktx:1.8.0'

    implementation composeBom
    implementation 'androidx.compose.runtime:runtime'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation composeBom
}



publishing {
    publications {
        release(MavenPublication) {
            groupId = GROUP_ID
            artifactId = "$ARTIFACT_ID_ROOT-$ARTIFACT_ID_SUFFIX"
            version = (VERSION + PRE_RELEASE_VERSION_SUFFIX)

            afterEvaluate {
                from components.release
            }
        }
    }
}