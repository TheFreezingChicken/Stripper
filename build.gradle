buildscript {
    def preCommitHookPath = ".git/hooks/pre-commit"

    // Check for pre-commit hook.
    if (!rootProject.file(preCommitHookPath).exists()) {
        throw new GradleException("Pre-commit hook is missing. Please make sure that ${preCommitHookPath} exists.")
    }


    ext {
        GROUP_ID = "me.thefreezingchicken.stripper"
        ARTIFACT_ID_PROJECT = "stripper"

        KOTLIN_COMPILER_VERSION = "1.8.10"
        COMPOSE_COMPILER_VERSION = "1.4.4"

        buildSuffix = { boolean isSnapshot, String replacementSuffix ->
            if (!isSnapshot) return replacementSuffix

            def headCommitHashSuffix = ""
            def workingTreeStatus = "git status --porcelain".execute([], project.rootDir).text.trim()
            def workingTreeClean = workingTreeStatus.isEmpty()

            if (workingTreeClean) {
                headCommitHashSuffix = "-" + ("git rev-parse --short HEAD".execute([], project.rootDir).text.trim())
            }

            return "-SNAPSHOT" + headCommitHashSuffix
        }
    }
}

// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '8.0.0' apply false
    id 'com.android.library' version '8.0.0' apply false
    id 'org.jetbrains.kotlin.android' version "$KOTLIN_COMPILER_VERSION" apply false
}