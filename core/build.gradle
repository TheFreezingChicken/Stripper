plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}


ext {
    ARTIFACT_ID_MODULE = 'core'
    VERSION = '1.0.1'
    def isSnapshot = false
    PRE_RELEASE_VERSION_SUFFIX = buildSuffix(isSnapshot, "")
}


android {
    namespace 'me.thefreezingchicken.stripper'
    compileSdk 33

    defaultConfig {
        minSdk 23
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
}



publishing {
    publications {
        release(MavenPublication) {
            groupId = GROUP_ID
            artifactId = "$ARTIFACT_ID_PROJECT-$ARTIFACT_ID_MODULE"
            version = (VERSION + PRE_RELEASE_VERSION_SUFFIX)

            pom {
                name = 'Stripper'
                description = 'Android library to add code blocks for debug, that are removed by ProGuard on release.'
                url = 'https://github.com/TheFreezingChicken/Stripper'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://thefreezingchicken.mit-license.org/'
                    }
                }
                developers {
                    developer {
                        id = 'TheFreezingChicken'
                        name = 'The Freezing Chicken'
                        email = 'thefreezingchicken+github@gmail.com'
                        url = 'http://thefreezingchicken.me/'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/TheFreezingChicken/Stripper.git'
                    developerConnection = 'scm:git:https://github.com/TheFreezingChicken/Stripper.git'
                    url = 'https://github.com/TheFreezingChicken/Stripper'
                }
            }


            repositories {
                maven {
                    name = "OSSRH"
                    setUrl("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
                    credentials {
                        username = keystoreProperties.mavenUsername
                        password = keystoreProperties.mavenPassword
                    }
                }
            }

            afterEvaluate {
                from components.release
            }
        }
    }
}